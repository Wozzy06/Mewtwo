apply plugin: 'java'
apply plugin: 'application'

version = '3.0.0-dev'
mainClassName = 'meew0.mewtwo.MewtwoMain'

repositories {
    mavenCentral()
    maven {
        url "https://oss.sonatype.org/content/repositories/snapshots"
    }
}

dependencies {
    compile "commons-configuration:commons-configuration:1.10"
    compile "org.apache.commons:commons-io:1.3.2"
    compile "com.google.guava:guava:18.0"
    compile "org.jruby:jruby-complete:9.0.0.0.pre1"
    compile "org.slf4j:slf4j-simple:1.7.7"
    compile "junit:junit:3.8.1"
    compile "org.projectlombok:lombok:1.14.8"
}

//noinspection GroovyAssignabilityCheck
task fatJar(type: Jar) {
    manifest {
        attributes 'Implementation-Title': 'Mewtwo',
                'Implementation-Version': version,
                'Main-Class': mainClassName
    }
    archiveName = project.name + '-all-dev.jar'
    from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
    //noinspection GroovyAssignabilityCheck
    with jar
}

task copyToLib(dependsOn: build, type: Copy) {
    into "lib"
    from configurations.runtime
}

//noinspection GroovyAssignabilityCheck
task buildGems(dependsOn: copyToLib) << {
    def mewtwoGemFile = new File('MewtwoGemfile')
    //noinspection GroovyMissingReturnStatement
    mewtwoGemFile.eachLine {
        if (!(it.startsWith('#') || it.isEmpty())) {
            downloadOneGem(it)
        }
    }
}

def downloadFile(String url, String path) {
    def f = new File(path)
    if (!f.exists()) {
        new URL(url).withInputStream{ i -> f.withOutputStream{ it << i }}
    }
}

def downloadOneGem(String target) {
    println "Downloading gem " + target
    exec {
        executable "java"
        args "-jar", ("lib/jruby-complete-1.7.16.1.jar"), "-S", "gem", "install", "-i", "lib/", target
    }
}
